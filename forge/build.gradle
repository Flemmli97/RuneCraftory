ext {
    shadow_dep = [group: "com.github.johnrengelman.shadow", name: "com.github.johnrengelman.shadow.gradle.plugin", version: "7.1.0"]
}

apply from: "https://raw.githubusercontent.com/Flemmli97/GradleScripts/mc-architectury-1.2/forge.gradle"

loom {
    forge {
        mixinConfigs = ["runecraftory.mixins.json", "runecraftory.forge.mixins.json"]
        dataGen {
            mod "runecraftory"
        }
        launches {
            data {
                arg "--existing", project(":common").file("src/main/resources").absolutePath
                arg "--existing", file("src/main/resources").absolutePath
            }
        }
    }
}

ext {
    mod_meta.tenshilib_version = tenshilib_version
}

// Make datagen output to common folder
configureLaunch.doLast {
    def file = file(".gradle/loom-cache/launch.cfg");
    def cfg = file.getText('UTF-8')
    cfg = cfg.replaceAll(project(":forge").file("src/generated/resources").absolutePath,
            project(":common").file("src/generated/resources").absolutePath)
    file.write(cfg, 'UTF-8')
}

repositories {
    mavenLocal()
    maven {
        name = "TenshiLib"
        url "https://gitlab.com/api/v4/projects/21830712/packages/maven"
    }
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        name = "JEI"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven { url "https://maven.shedaniel.me" }
    maven {
        name = "CurseMaven"
        url "https://www.cursemaven.com"
    }
    maven {
        name = "minecolonies"
        url "https://ldtteam.jfrog.io/ldtteam/modding/"
    }
}

dependencies {
    modImplementation "io.github.flemmli97:tenshilib:${tenshilib_version}-forge"
    modImplementation patchouli
    modImplementation patchouli_datagen
    modImplementation "${simplequests}-forge"
    modCompileOnly "${jei_dep}:api"
    modRuntimeOnly jei_dep

    modCompileOnly "com.ldtteam:minecolonies:${rootProject.minecolonies}"

    //Mods useful for testing things
    modRuntimeOnly "curse.maven:mobbattle-268746:3952783"
    modRuntimeOnly "curse.maven:dummy-225738:3820503"
    modRuntimeOnly "curse.maven:spark-361579:3824951"

    modRuntimeOnly "io.github.flemmli97:debugutils:1.18.2-1.0.1-forge"
}